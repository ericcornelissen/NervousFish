apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply from: '../config/quality.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.nervousfish.nervousfish"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testApplicationId "com.nervousfish.nervousfish.test"
        testInstrumentationRunner "com.nervousfish.nervousfish.test.Instrumentation"
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/JacocoReportDir")
}

task jacocoTestReport(type: JacocoReport, dependsOn: "connectedDebugAndroidTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/reports/jacoco/test/html"
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: [
                    // Exclude dependencies
                    'android/**/*.class', 'cn/pedant/**/*.class', 'com/pnikosis/**/*.class',

                    // Exclude hard to test code
                    'com/nervousfish/nervousfish/modules/pairing/*.class' // Bluetooth/NFC connections
            ]
    )
    sourceDirectories = files(['src/main/java'])
    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'cn.pedant.sweetalert:library:1.3'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.zxing:javase:3.3.0'
    compile 'com.google.zxing:core:3.3.0'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.22'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'info.cukes:cucumber-android:1.2.2@jar'
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.2'
}
